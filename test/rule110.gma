dummy bool := false

LEN :: 150                  // amount of lines printed

prev :: &dummy
cur  :: prev + LEN          // size of prev is LEN*BOOL_SIZE


fn memcpy(dest *bool, src *bool, size i32) {
    for i i32, size {
        *(dest+i) = *(src+i)
    }
}

fn printRow() {
    for i i32, LEN {
        if *(cur+i) {
            printStr("o")
        } else {
            printStr(" ")
        }
    }

    printStr("\n")
}

fn setNextRow(round i32) {
    *(cur+LEN-round-1) = true

    for i i32, LEN-1, 1 {
        if *(prev+i-1) == false {
            if *(prev+i) == false {
                if *(prev+i+1) == false {       // 000 -> 0
                    *(cur+i) = false
                } else {                        // 001 -> 1
                    *(cur+i) = true
                }
            } else {
                *(cur+i) = true                 // 010 -> 1 / 011 -> 1
            }
        } else {
            if *(prev+i) == false {
                if *(prev+i+1) == false {       // 100 -> 0
                    *(cur+i) = false
                } else {                        // 101 -> 1
                    *(cur+i) = true
                }
            } else {
                if *(prev+i+1) == false {       // 110 -> 1
                    *(cur+i) = true
                } else {                        // 111 -> 0
                    *(cur+i) = false
                }
            }
        }
    }

    memcpy(prev, cur, LEN)
}

fn main() {
    for i i32, LEN {
        setNextRow(i)
        printRow()
    }
}
